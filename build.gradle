/*
 * Copyright 2020-2020 Exactpro (Exactpro Systems Limited)
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 * http://www.apache.org/licenses/LICENSE-2.0
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id 'java'
    id 'org.jetbrains.kotlin.jvm' version '1.3.71'
    id 'application'
    id 'com.palantir.docker' version '0.25.0'
}

ext {
    dockerImageVersion = "${version_major}.${version_minor}.${version_maintenance}.${version_build}"
    dockerImageLastVersion = "${version_major}.${version_minor}-latest"
    genDir             = file('src/gen')
    sharedDir          = file("${project.rootDir}/shared")
}

group 'com.exactpro.th2'
version "${version_major}.${version_minor}.${version_maintenance}"

repositories {
    maven {
        name 'MavenLocal'
        url sharedDir
    }
    mavenCentral()
    maven {
        name 'Snapshots'
        url 'http://artifactory5.exp.exactpro.com/artifactory/th2-schema-snapshot-local'
    }
    maven {
        name 'Artifactory'
        url 'http://artifactory5.exp.exactpro.com/artifactory/libs-snapshot'
    }

    configurations.all {
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
        resolutionStrategy.cacheDynamicVersionsFor 0, 'seconds'
    }
}


dependencies {

    implementation 'io.github.microutils:kotlin-logging:1.7.+'

    implementation 'org.slf4j:slf4j-api:1.7.+'
    implementation 'org.slf4j:slf4j-log4j12:1.7.+'

    implementation 'com.fasterxml.jackson.core:jackson-core:2.9.+'
    implementation 'com.fasterxml.jackson.module:jackson-module-kotlin:2.9.+'

    implementation 'org.jetbrains.kotlin:kotlin-stdlib-jdk8'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.7'

    implementation 'com.exactpro.th2:th2-grpc-infra:1.1-SNAPSHOT'
    implementation 'com.exactpro.th2:th2-common:1.1-SNAPSHOT'

    implementation "com.exactpro.th2:th2-grpc-event-store:1.1-SNAPSHOT"
    implementation("com.github.ajalt:clikt:2.7.0")

    implementation 'com.google.protobuf:protobuf-java-util:3.12.+'
}

distTar {
    archiveName "${archivesBaseName}.tar"
}

dockerPrepare {
    dependsOn distTar
}

docker {
    name "${target_docker_repository}/${docker_image_name}:${dockerImageVersion}"
    tag 'local-version', "${docker_image_name}:${dockerImageVersion}"
    tag 'local-latest', "${docker_image_name}:${dockerImageLastVersion}"
    tag 'remote-latest',  "${target_docker_repository}/${docker_image_name}:${dockerImageLastVersion}"

    copySpec.from(tarTree("$buildDir/distributions/${archivesBaseName}.tar"))
}

compileKotlin {
    kotlinOptions.jvmTarget = "11"
}

application {
    mainClassName = "com.exactpro.th2.codec.MainKt"
}

